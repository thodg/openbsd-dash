diff --git a/configure.ac b/configure.ac
index fe7382891..08413e17e 100644
--- configure.ac.orig
+++ configure.ac
@@ -171,6 +171,12 @@ AC_ARG_WITH([system-univalue],
   [system_univalue=$withval],
   [system_univalue=no]
 )
+AC_ARG_WITH([system-secp256k1],
+  [AS_HELP_STRING([--with-system-secp256k1],
+  [Build with system Secp256k1 (default is no)])],
+  [system_secp256k1=$withval],
+  [system_secp256k1=no]
+)
 AC_ARG_ENABLE([zmq],
   [AS_HELP_STRING([--disable-zmq],
   [disable ZMQ notifications])],
@@ -915,6 +921,54 @@ AM_CONDITIONAL([EMBEDDED_UNIVALUE],[test x$need_bundled_univalue = xyes])
 AC_SUBST(UNIVALUE_CFLAGS)
 AC_SUBST(UNIVALUE_LIBS)
 
+dnl secp256k1 check
+
+need_bundled_secp256k1=yes
+
+if test x$build_bitcoin_utils$build_bitcoind$bitcoin_enable_qt$use_tests$use_bench = xnonononono; then
+  need_bundled_secp256k1=no
+else
+
+if test x$system_secp256k1 != xno ; then
+  found_secp256k1=no
+  if test x$use_pkgconfig = xyes; then
+    : #NOP
+    m4_ifdef(
+      [PKG_CHECK_MODULES],
+      [
+        PKG_CHECK_MODULES([SECP256K1],[libsecp256k1],[found_secp256k1=yes],[true])
+      ]
+    )
+  else
+    AC_CHECK_HEADER([secp256k1.h],[
+      AC_CHECK_LIB([secp256k1],  [main],[
+        SECP256K1_LIBS=-lsecp256k1
+        found_secp256k1=yes
+      ],[true])
+    ],[true])
+  fi
+
+  if test x$found_secp256k1 = xyes ; then
+    system_secp256k1=yes
+    need_bundled_secp256k1=no
+  elif test x$system_secp256k1 = xyes ; then
+    AC_MSG_ERROR([secp256k1 not found])
+  else
+    system_secp256k1=no
+  fi
+fi
+
+if test x$need_bundled_secp256k1 = xyes ; then
+  SECP256K1_CFLAGS='-I$(srcdir)/secp256k1/include'
+  SECP256K1_LIBS='secp256k1/libsecp256k1.la'
+fi
+
+fi
+
+AM_CONDITIONAL([EMBEDDED_SECP256K1],[test x$need_bundled_secp256k1 = xyes])
+AC_SUBST(SECP256K1_CFLAGS)
+AC_SUBST(SECP256K1_LIBS)
+
 BITCOIN_QT_PATH_PROGS([PROTOC], [protoc],$protoc_bin_path)
 
 AC_MSG_CHECKING([whether to build dashd])
@@ -1144,8 +1198,10 @@ if test x$need_bundled_univalue = xyes; then
   AC_CONFIG_SUBDIRS([src/univalue])
 fi
 
-ac_configure_args="${ac_configure_args} --disable-shared --with-pic --with-bignum=no --enable-module-recovery"
-AC_CONFIG_SUBDIRS([src/secp256k1])
+if test x$need_bundled_secp256k1 = xyes; then
+  ac_configure_args="${ac_configure_args} --disable-shared --with-pic --with-bignum=no --enable-module-recovery"
+  AC_CONFIG_SUBDIRS([src/secp256k1])
+fi
 
 AC_OUTPUT
 
